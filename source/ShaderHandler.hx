#if !flash 
import flixel.addons.display.FlxRuntimeShader;
import openfl.filters.ShaderFilter;
#end
import FunkinLua;
import flixel.system.FlxSound;
import flixel.util.FlxSave;


#if sys
import sys.FileSystem;
import sys.io.File;
#end
import flixel.FlxG;

class ShaderHandler
{
  #if (!flash && sys)
  public static var runtimeShaders:Map<String, Array<String>> = new Map<String, Array<String>>();
  public static function createRuntimeShader(name:String):FlxRuntimeShader
  {
    if(!ClientPrefs.shaders) return new FlxRuntimeShader();

    #if (!flash && MODS_ALLOWED && sys)
    if(!runtimeShaders.exists(name) && !initLuaShader(name))
    {
      FlxG.log.warn('Shader $name is missing!');
      return new FlxRuntimeShader();
    }

    var arr:Array<String> = runtimeShaders.get(name);
    return new FlxRuntimeShader(arr[0], arr[1]);
    #else
    FlxG.log.warn("Platform unsupported for Runtime Shaders!");
    return null;
    #end
  }

  public static function initLuaShader(name:String, ?glslVersion:Int = 120)
  {
    if(!ClientPrefs.shaders) return false;

    if(runtimeShaders.exists(name))
    {
      FlxG.log.warn('Shader $name was already initialized!');
      return true;
    }

    var foldersToCheck:Array<String> = [Paths.mods('shaders/')];
    if(Paths.currentModDirectory != null && Paths.currentModDirectory.length > 0)
      foldersToCheck.insert(0, Paths.mods(Paths.currentModDirectory + '/shaders/'));

    for(mod in Paths.getGlobalMods())
      foldersToCheck.insert(0, Paths.mods(mod + '/shaders/'));
    
    for (folder in foldersToCheck)
    {
      if(FileSystem.exists(folder))
      {
        var frag:String = folder + name + '.frag';
        var vert:String = folder + name + '.vert';
        var found:Bool = false;
        if(FileSystem.exists(frag))
        {
          frag = File.getContent(frag);
          found = true;
        }
        else frag = null;

        if (FileSystem.exists(vert))
        {
          vert = File.getContent(vert);
          found = true;
        }
        else vert = null;

        if(found)
        {
          runtimeShaders.set(name, [frag, vert]);
          //trace('Found shader $name!');
          return true;
        }
      }
    }
    FlxG.log.warn('Missing shader $name .frag AND .vert files!');
    return false;
  }
  #end
}